if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt")
  return()
endif()

file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")
foreach(file ${files})
  message(STATUS "Uninstalling $ENV{DESTDIR}${file}")
  if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    execute_process(COMMAND
      "@CMAKE_COMMAND@" -E remove "$ENV{DESTDIR}${file}"
      OUTPUT_VARIABLE out_var
      RESULT_VARIABLE res_var
      )
    if(NOT "${res_var}" STREQUAL 0)
      message(FATAL_ERROR "Problem when removing $ENV{DESTDIR}${file}")
    endif()
  else()
    message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
  endif()
endforeach(file)

function(uninstall_dir dir_path)
  message(STATUS "Uninstalling ${dir_path}")
  if(EXISTS "${dir_path}" AND IS_DIRECTORY "${dir_path}")
    execute_process(COMMAND
      "@CMAKE_COMMAND@" -E remove_directory "${dir_path}"
      OUTPUT_VARIABLE out_rmdir
      RESULT_VARIABLE res_rmdir
      )
    if(NOT "${res_rmdir}" STREQUAL 0)
      message(FATAL_ERROR "Problem when removing ${dir_path}")
    endif()
  endif()  
endfunction()

foreach(camera @MAJA_LIST_OF_SPECTRAL_CAMERA@)
  foreach(share_dir "config" "examples" "schemas")
    uninstall_dir("@CMAKE_INSTALL_PREFIX@/share/${share_dir}/${camera}")
  endforeach()
endforeach()

foreach(extra_dir
    "etc/conf" "share/schemas/Maja"
    "share/ddc" "share/config/Fonts" "share/config/Templates")
  uninstall_dir("@CMAKE_INSTALL_PREFIX@/${extra_dir}")
endforeach()

foreach(include_dir @MAJA_INCLUDE_RELATIVE_DIRS@)
  uninstall_dir( "@CMAKE_INSTALL_PREFIX@/include/${include_dir}")
endforeach()

uninstall_dir( "@CMAKE_INSTALL_PREFIX@/include/TestingMiscellaneous")
